program = ws varDeclList:vdl ws body:b ws {NemoProg(:vdl, :b)};

body = statement;
statement = (ws alt:a ws ';' {:a})*:as {buildBodiesSeq(:as)};
alt = bodyTerm:t choiceElement*:es ws {buildUDChoice(:t, :es)};
choiceElement = ws udChoiceSymbol ws bodyTerm:t {:t};
bodyTerm = simpleBodyTerm:t {:t} | '{' ws statement:s ws '}' {:s};
simpleBodyTerm = printOperation| assignment | test | iteration;
printOperation = print ws '(' ws expr:e ws ')' {Print(:e)};
assignment = varName:n ws ":=" ws expr:e {Assign(:n, :e)};
test = '(' ws boolExpr:be ws ')' ws '?' {Test(:be)};
iteration = '{' ws body:b ws '}' ws '*' {UDIterate(:b)};

expr = boolExpr | arithmExpr | varExpr | arrayOp | const;

boolExpr = ws orOp:o ws {:o};
orOp = andOp:a ws or ws orOp:o {Or(:a, :o)} | andOp:a {:a};
andOp = boolTerm:t ws and ws andOp:a {buildAnd(:t, :a)} | boolTerm:t {:t};
boolTerm = boolTermWithoutBrackets:t {:t} | '(' ws boolTerm:t ws ')' {:t};

boolTermWithoutBrackets = boolVar:t1 ws '=' ws boolVar:t2 {buildEqual(:t1, :t2)}
    | boolVar:t1 ws '<' ws boolVar:t2 {buildLess(:t1, :t2)}
    | boolVar:t1 ws "<=" ws boolVar:t2 {buildLessOrEqual(:t1, :t2)}
    | boolVar:t1 ws ">" ws boolVar:t2 {buildGreater(:t1, :t2)}
    | boolVar:t1 ws ">=" ws boolVar:t2 {buildGreaterOrEqual(:t1, :t2)}
    | boolVar:t1 ws "<>" ws boolVar:t2 {buildNotEqual(:t1, :t2)}
    | not ws boolExpr:e {Not(:e)}
    | '(' ws boolExpr:e ws ')' {:e}
    | boolConst:c {:c};

boolVar = arithmExpr
    | varExpr
    | arrayOp
    | const;

varExpr = varName:n {Var(:n)};

arithmExpr = ws secondpriority:e ws {:e};

secondpriority = firstpriority:e1 subtrahend*:es ws '+' ws secondpriority:e2 {ABin(buildASub(:e1, :es),:e2, AAddOp())}
    | firstpriority:e1 subtrahend*:es {buildASub(:e1, :es)};

firstpriority = term:e1 divisor*:es ws '*' ws firstpriority:e2 {ABin(buildADiv(:e1, :es),:e2, AMulOp())}
    | term:e1 divisor*:es {buildADiv(:e1, :es)};

divisor = ws '/' ws term:e {:e};
subtrahend = ws '-' ws firstpriority:e {:e};

term = '+'? const:e {:e}
    | '-' const:e {ANeg(:e)}
    | '+'? varExpr:e {:e}
    | '-' varExpr:e {ANeg(:e)}
    | '+'? arrayOp:e {:e}
    | '-' arrayOp:e {ANeg(:e)}
    | '+'? '(' ws expr:e ws ')' {:e}
    | '-(' ws expr:e ws ')' {ANeg(:e)};

arrayOp = arrayUpdate
    | arrayApply
    | arrayConcat
    | arrayLength;

arrayUpdate = update ws '(' ws expr:a ws ',' ws expr:i ws ',' ws expr:v ws ')' {ArrOp(:a, ArrUpdateOp(:i, :v))};
arrayApply = apply ws '(' ws expr:a ws ',' ws expr:i ws ')' {ArrOp(:a, ArrApplyOp(:i))};
arrayConcat = concat ws '(' ws expr:a1 ws ',' ws expr:a2 ws ')' {ArrOp(:a, ArrConcatOp(:a2))};
arrayLength = length ws '(' ws expr:a ws ')' {ArrOp(:e, ArrLengthOp())};

const = realConst
    | intConst
    | stringConst
    | boolConst
    | arrayConst;

intConst = intNumber$d {IntConst(s2i($d))};
stringConst = '"' (onechar*)$s '"' {StringConst(:s)};

boolConst = true {buildTrueConst()}
    | false {buildFalseConst()};

realConst = (intNumber '.' posIntNumber)$s {RealConst(s2d(:s))};

arrayConst = '[' ws ']'{buildArrayConst()}
    | '[' ws constEnum*:cs const?:c ws ','? ws ']' {buildArrayConst(:cs, :c)};

constEnum = const:c ws ',' ws {:c};

varDeclList = varDecl*:ds {buildVarDecls(:ds)};
varDecl = ws var s+ varName:n ws ':' ws type:t ws ';' ws {VarDecl(:n, :t)};

type = intType
    | strType
    | boolType
    | realType
    | arrayType;

intType = int {Int()};
strType = str {String()};
boolType = bool {Bool()};
realType = real {Real()};
arrayType = '[' ws type:t ws ']' {Array(:t)};

keyword = var
    | int
    | str
    | bool
    | real
    | true
    | false
    | update
    | apply
    | concat
    | length
    | udChoiceSymbol
    | print
    | not
    | and
    | or;

var = "VAR";
int = "INT";
str = "STR";
bool = "BOOL";
real = "REAL";
true = "true";
false = "false";
update = "UPD";
apply = "APP";
concat = "CONCAT";
length = "LENGTH";
udChoiceSymbol = "U";
print = "PRINT";
not = "NOT";
and = "AND";
or = "OR";

varName = !keyword (letter (letter | digit)*)$n {:n};
intNumber = '-'? posIntNumber;
posIntNumber = digit+;
letter = 'a' - 'z' | 'A' - 'Z';
digit = '0'-'9';
ws = s*;
s = ' ' | '\t' | '\n';

onechar = "\\u" hexdigit hexdigit hexdigit hexdigit
    | "\\X" hexdigit hexdigit hexdigit hexdigit
	| "\\x" hexdigit hexdigit
	| '\\' escapedchar
	| !'"' !'\\' char;

hexdigit = digit
	| 'a'-'f'
	| 'A'-'F';

escapedchar = "\\" | '"' | "n" | "t" | "r";
char = '\u0000'-'\uFFFF';
